import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

ext {
    majorVersion = "1.8.2"
    buildVersion = "DEV-" + System.getenv("BUILD_NUMBER")
    release = "Release"
}

group 'me.clip'
version majorVersion + "-" + buildVersion
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'

    implementation 'com.github.cryptomorin:XSeries:8.2.0'
}

shadowJar {
    minimize()
    relocate 'com.cryptomorin.xseries', 'me.clip.deluxetags.libs.xseries'
    archiveFileName = "DeluxeTags-${project.version}.jar"
}

configurations {
    testImplementation {
        extendsFrom(compileOnly)
    }
}

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}